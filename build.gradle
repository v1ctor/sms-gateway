plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.4.21'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'application'
}

group 'org.buldakov.sms.gateway'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
    jcenter { url "https://jcenter.bintray.com" }
    maven { url "https://kotlin.bintray.com/kotlinx" }
}

ext {
    retrofit_version = '2.3.0'
    okhttp_version = '4.9.0'
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib"

    //Sms modem client
    implementation("org.buldakov.huawei.modem.client:huawei-api-client:1.0.9")

    //Telegram client
    implementation "io.github.kotlin-telegram-bot.kotlin-telegram-bot:telegram:6.0.4"

    implementation "org.jetbrains.exposed:exposed-core:0.28.1"
    implementation "org.jetbrains.exposed:exposed-jdbc:0.28.1"
    implementation "org.xerial:sqlite-jdbc:3.30.1"

    implementation "org.jetbrains.kotlinx:kotlinx-cli:0.3"

    implementation "joda-time:joda-time:2.10.5"

    implementation "ch.qos.logback:logback-classic:1.2.3"
    implementation "org.slf4j:slf4j-api:1.6.1"
}


configurations.all {
    resolutionStrategy.eachDependency { details ->
        if (details.requested.group == 'com.squareup.okhttp3') {
            details.useVersion rootProject.ext.okhttp_version
        }
        if (details.requested.group == 'com.squareup.retrofit2') {
            details.useVersion rootProject.ext.retrofit_version
        }
    }
}

compileKotlin { kotlinOptions.jvmTarget = "1.8" }
compileTestKotlin { kotlinOptions.jvmTarget = "1.8" }

application {
    mainClassName = 'org.buldakov.sms.gateway.MainKt'
}

shadowJar {
    archiveBaseName.set("sms-gateway")
    archiveClassifier.set('')
    archiveVersion.set('')
}
